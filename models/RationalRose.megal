module RationalRose

import UML
import XML
import java.JavaPlatform

/* Rational Rose supports UML Modeling. A user can create a model in a 
visual notation and save it in the specific .mdl format. No
schema was released for MDL, but it is supposedly a subset of XML. */
Rose.MDL : FileFormat
    = "http://dl.acm.org/citation.cfm?id=579213"
    = "https://www.ibm.com/developerworks/community/forums/html/topic?id=77777777-0000-0000-0000-000013786606"
    subsetOf XML
Rose.UML : ModelingLanguage
    = "http://dl.acm.org/citation.cfm?id=579213"
    subsetOf UML
RationalRose : Software
    = "http://www-03.ibm.com/software/products/en/enterprise"
    = "http://dl.acm.org/citation.cfm?id=579213"
    uses Java
    implements Rose.MDL
    implements Rose.UML
    supports ModelDrivenEngineering
    belongsTo MDEWare

/* Class diagrams can be created and edited with RationalRose 
and then saved in the RoseMDL format.
Rose does presumably not support the full set of UML diagrams.
We could not find evidence for it supporting edge classes. */
?Rose.UML.ClassDiagram : ModelingLanguage
    subsetOf Rose.UML
?umlClassDiagram : Artifact
    elementOf ?Rose.UML.ClassDiagram
    hasRole Schema
    manifestsAs Transient
?roseMDLFile : Artifact
    elementOf Rose.MDL
    hasRole Schema
    manifestsAs File
    correspondsTo ?umlClassDiagram
saveMDL : Rose.UML -> Rose.MDL
saveMDL(?umlClassDiagram) |-> ?roseMDLFile
RationalRose implements saveMDL
loadMDL : Rose.MDL -> Rose.UML
loadMDL(?roseMDLFile) |-> ?umlClassDiagram
RationalRose implements loadMDL